--*****************************************************************************
--*                                S Y N T A X
--*----------------------------------------------------------------------------
--*   Copyright (C) 1972-2023 INRIA (Institut National de Recherche en
--*   Informatique et Automatique)
--*----------------------------------------------------------------------------
--*   URL: http://sourcesup.renater.fr/projects/syntax
--*----------------------------------------------------------------------------
--*   The source code of SYNTAX is distributed with two different licenses,
--*   depending on the files:
--*   - The recursive content of src/ and incl/ and the non-recursive content
--*     of SYNTAX's root directory are distributed under the CeCILL-C license
--*   - The recursive content of all other repertories is distributed under
--*     the CeCILL license
--*   All code produced by SYNTAX must be considered as being under the
--*   CeCILL-C license. Information about the CeCILL and CeCILL-C licenses
--*   can be found at, e.g., http://www.cecill.info
--*****************************************************************************

-- Convertisseur de sources YACC en sources YAX

Classes

	ANY		= #000..#377 ;
	Y_LETTER	= LETTER + "._" ;
	SPACE		= " \t\n" ;


Abbreviations

	Y_COMMENT_TO_BNF= -"/" @Put (EOL) "*"&True {^"*" | @Mark "*"} @Mark "*" @Erase -"/"&True @Put (EOL) ;
	ID		= Y_LETTER {Y_LETTER | DIGIT} ;
	OCTAL		= DIGIT [DIGIT [DIGIT]] ;
	C_STRING	= "\"" {^"\"\n\\" | "\\" ANY} "\"" ;
	C_CHAR		= "\'" {^"\'\n\\" | "\\" ANY} "\'" ;
	C_COMMENT	= "/" "*"&True {ANY} "*" "/"&True ;
	C_CODE		= {^"{}\"'/" | C_STRING | C_CHAR | "/" | C_COMMENT}+ ;
	TEXT		= "{" @Reset(0) {C_CODE | "{" @Incr(0) | "}"&Is_Set(0) @Decr(0)} "}" ;


Tokens

	Comments	= {-SPACE | Y_COMMENT_TO_BNF}+ ;
			   Context All But Comments ;
	%ID		= ID ;
			   Priority Shift>Reduce ;
			   Unbounded Context All But NUM ;
	%C_ID		= ID ;
			   Priority Shift>Reduce ;
			   Unbounded Context All ;
	%LIT		=
	     Union
		CHAR	: -"\'" @Put ("\"")
			  (^"\"\\"
			   | @Put ("\\") "\""
			   | @Mark "\\" ("bnfrt\\" | @Erase "'" | OCTAL)
			  )
			  -"\'" @Put ("\"") ;
		STRING	: "\"" (^"\\" | "\\" ("bnfrt\"\\" | OCTAL)) "\"" ;
	     End ;
	%ACTION		=
	     Union
		NORMAL	: ("=" | @Put ("=")) TEXT ;
		SYMPA	: "=" @Put ("{") C_CODE ";"&True @Put ("}") ;
	     End ;
	NUM		= -{DIGIT}+ ;
	KEY_WORD	= "%\\" ({LETTER}+ | "<>02=") ;
			   Priority Shift>Reduce ;
	%UNION		= "%\\" "u" "n" "i" "o" "n"&True @Erase {^"{"} TEXT ;
	%MARK		=
	     Union
		FIRST	: "%\\" "%\\" @Set (1) @Erase ;
		SECOND	: "%\\" "%\\"&Is_Set (1) @Erase {ANY&True} ;
			Context Eof ;
	     End ;
	%LCURL		= "%\\" TEXT ;


Synonyms

	"%left"		= "\\left", "%<", "\\<" ;
	"%right"	= "\\right", "%>", "\\>" ;
	"%nonassoc"	= "\\nonassoc", "%binary", "\\binary", "%2", "\\2" ;
	"%token"	= "\\token", "%term", "\\term", "%0", "\\0" ;
	"%prec"		= "\\prec", "%=", "\\=" ;
	"%type"		= "\\type" ;
	"%start"	= "\\start" ;
