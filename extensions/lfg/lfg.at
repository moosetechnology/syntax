*
* BNF fondee sur la xlfg de Lionel Clement
*

* Pour le test
<axiome>		= <declaration_LIST> <rule_LIST> ;				"LFG"

* #############################################################
* #DECLARATION DES OBJETS DE LA GRAMMAIRE LEXICALE
* #############################################################

<declaration_LIST>	= <declaration_LIST> <declaration> ;
<declaration_LIST>	= ;								"DECLARATION_S"

<declaration>		= ATTRIBUTE <attribut_LIST> ;					"ATTRIBUTE"
<declaration>		= ARGUMENT <argument_LIST> ;					"ARGUMENT"
<declaration>		= CATEGORY <argument_LIST> ;					"CATEGORY"
<declaration>		= TERMINAL <argument_LIST> ;					"TERMINAL"
<declaration>		= AMALGAM <amalgam_LIST> ;					"AMALGAM"
<declaration>		= COMPOUND <compound_LIST> ;					"COMPOUND"
<declaration>		= %MACRO_NAME "=" <structure> ";" ;				"MACRO_STRUCTURE"
* Le 20/06/05 Ca permet specifier la liste des non-terminaux dont les f_structures doivent etre complete et coherentes
<declaration>		= COMPLETE <pair_LIST> ;					"COMPLETE"

<argument_LIST>		= <argument_LIST> <graphie> ";" ;
<argument_LIST>		= <graphie> ";" ;						"ARGUMENT_S"

<pair_LIST>		= <pair_LIST> <pair> ";" ;
<pair_LIST>		= <pair> ";" ;							"PAIR_S"

<pair>                  = <graphie> ;
<pair>                  = <graphie> %CHAINE ;						"PAIR"	

<attribut_LIST>		= <attribut_LIST> <attribut_pass_nb> ";" ;
<attribut_LIST>		= <attribut_pass_nb> ";" ;					"ATTRIBUTE_S"

<attribut_pass_nb>	= <attribut> <pass_nb> ;					"ATTRIBUTE_PASS_NB"

<attribut>		= %IDENT "=" "{" <disjonction_LIST> "}" ;			"ATTR_ATOMS"
<attribut>		= %IDENT "=" "{" <disjonction*> "." "." "." "}" ;		"ATTR_DOTS"
<attribut>		= %IDENT "=" "[" "]" ;						"ATTR_STRUCT"
<attribut>		= %IDENT "=" "<" "[" "]" ">" ;					"ATTR_ARG_STRUCT"
<attribut>		= %IDENT "=" "{" "[" "]" "}" ;					"ATTR_SET_OF_STRUCT"

* Le 10/05/05 Ca permet de specifier le No de la passe au cours de laquelle le champ est e'value'
<pass_nb>               = ;								"VOID"
<pass_nb>               = %ENTIER ;
<pass_nb>               = "," %ENTIER ;

<disjonction*>		= <disjonction_LIST> ;
<disjonction*>		= ;

<structure>		= "[" <attr_val_LIST> "]" ;
<structure>		= "[" <attr_val_LIST> "]" %ENTIER ;				"NAMED_STRUCTURE"
<structure>		= "[" "]" ;							"EMPTY_STRUCT"
<structure>		= "[" "]" %ENTIER ;						"EMPTY_NAMED_STRUCT"

<attr_val_LIST> 	= <attr_val_LIST> "," <attr_val> ;
<attr_val_LIST> 	= <attr_val> ;							"ATTR_VAL_S"

<attr_val> 		= <attr> <field_oper> <val> ;					"ATTR_VAL"
<attr_val> 		= pred <field_oper> "\"" <graphie> <sous_cat> "\"" ;		"PRED_FIELD"
<attr_val> 		= %MACRO_NAME ;							"MACRO"

<attr>			= %IDENT ;							"FIELD"	
<attr>			= "(" %IDENT ")" ;						"OPT_FIELD"

<field_oper>		= "=" ;								"FIELD_EQUAL"
<field_oper>		= CONTRAINT ;							"FIELD_CONSTRAINT"
<field_oper>		= OPTIONAL ;							"FIELD_OPTIONAL"

<val>			= <structure> ;
<val>			= <atom_def_LIST> ;
<val>			= "(" <atom_def_LIST> ")" %ENTIER ;					"NAMED_ATOM"
<val>			= "(" ")" ;							"EMPTY_ATOM"
<val>			= "(" ")" %ENTIER ;						"EMPTY_NAMED_ATOM"
<val>			= "{" <structure_LIST> "}" ;
<val>			= "{" <structure_LIST> "}" %ENTIER ;				"NAMED_ADJUNCT"
<val>			= "{" "}" ;							"EMPTY_ADJUNCT"
<val>			= "{" "}" %ENTIER ;						"EMPTY_NAMED_ADJUNCT"

<structure_LIST>	= <structure_LIST> "," <structure> ;
<structure_LIST>	= <structure> ;							"STRUCTURE_S"

<sous_cat>		= "<" <disjonction_LIST> ">" ;
<sous_cat>		= "<" <disjonction_LIST> ">" <disjonction_LIST> ;		"SOUS_CAT"
<sous_cat>		= ;								"EMPTY_SOUS_CAT"

<disjonction_LIST>	= <disjonction_LIST> "," <disjonction> ;
<disjonction_LIST>	= <disjonction> ;						"DISJONCTION_S"

<disjonction>		= "(" <disjonction> ")" ;					"OPT_DISJONCTION"
<disjonction>		= <atom_def_LIST> ;

<amalgam_LIST>		= <amalgam_LIST> <ident2_LIST> ";" ;
<amalgam_LIST>		= <ident2_LIST> ";" ; 						"AMALGAM_S"

<compound_LIST>		= <compound_LIST> <ident2_LIST> ";" ;
<compound_LIST>		= <ident2_LIST> ";" ; 						"COMPOUND_S"

<ident2_LIST>		= <ident2_LIST> <word> ;
<ident2_LIST>		= <word> <word> ; 						"IDENT_S"

<word>			= %IDENT ;
<word>			= %CHAINE ;

* RULE_PART
<rule_LIST>		= <rule_LIST> <rule> ;
<rule_LIST>		= <rule> ; 							"RULES_S"

<rule>			= <vocabulary_LIST> ";" <equation_LIST> ;			"RULE"

<vocabulary_LIST>	= <vocabulary_LIST> <vocabulary> ;
<vocabulary_LIST>	= %MOT_1COL "-" ">" ;						"VOCABULARY_S"

<vocabulary>		= %IDENT ;
<vocabulary>		= "(" %IDENT ")" ;						"OPT_NON_TERMINAL"

<equation_LIST>		= <equation_LIST> <equation> ";" ;
<equation_LIST>		= ;								"EQUATION_S"

<equation>		= %NODE_REF ":" <equation> ;					"ETIQUETTE"
<equation>		= "?" "(" <unifier> ")" ;					"POSSIBLE"
<equation>		= <unifier> ;

<unifier>		= "(" <unifier> ")" ;
<unifier>		= "~" <unifier> ; 						"NON"
<unifier>		= "+" <lhs> ; 							"EXISTE"
<unifier>		= "-" <lhs> ; 							"NONEXISTE"
<unifier>		= <lhs> <eq_oper> <rhs> ;					"UNIFIER"

<lhs>			= <$$_chemin> ;

<eq_oper>		= "=" ;								"UNIF_OPER"
<eq_oper>		= "CONTRAINT" ;							"CONTRAINTE_OPER"
<eq_oper>		= "<" ;								"DANS_OPER"
<eq_oper>		= "ADD" ;							"ADD_OPER"
<eq_oper>		= "OPTIONAL" ;							"OPTION_OPER"

<rhs>			= "&lex" %NODE_REF ;						"LEX_NODE_REF"
<rhs>			= "&lexeme" %NODE_REF ;						"LEXEME_NODE_REF"
<rhs>			= <$$_chemin> ;
<rhs>			= <atom_LIST> ;
<rhs>			= "[" "]" ;							"EMPTY_STRUCT"
<rhs>			= "(" ")" ;							"EMPTY_ATOM"
<rhs>			= "{" "}" ;							"EMPTY_ADJUNCT"

<$$_chemin> 		= <concat> ;
<$$_chemin> 		= <concat_er> ;

<atom_def_LIST>		= <atom_def_LIST> "|" <atome_def> ;
<atom_def_LIST>		= <atome_def> ;							"ATOM_S"

<atom_LIST>		= <atom_LIST> "|" <atome> ;
<atom_LIST>		= <atome> ;							"ATOM_S"

<atome_def>			= <champ> ;
<atome_def>			= "+" ;								"ATOM_PLUS"
<atome_def>			= "-" ;								"ATOM_MINUS"

<atome>			= <champ> ;
<atome>			= "+" ;								"ATOM_PLUS"
<atome>			= "-" ;								"ATOM_MINUS"
<atome>			= "&" <concat> "-" <term> ;					"ATOM_CONCAT"

<concat>		= "(" <concat> ")" ;
<concat>		= <concat> <term> ;						"TERME_CHEMIN"
<concat>		= <concat> "." <term> ;						"TERME_CHEMIN_DOT"
<concat>		= <concat> "-" <term> ;						"TERME_CHAMP"
<concat>		= %NODE_REF ;

<concat_er>		= "(" <concat_er> ")" ;
<concat_er>		= "{" <concat_er> "}" ;
<concat_er>		= <concat_er> <term> ;						"TERME_CHEMIN_RE"
<concat_er>		= <concat_er> "." <term> ;					"TERME_CHEMIN_RE_DOT"
<concat_er>		= <concat_er> <term_er> ;					"TERME_CHEMIN_RE"
<concat_er>		= <concat_er> "." <term_er> ;					"TERME_CHEMIN_RE_DOT"
<concat_er>		= <concat> <term_er> ;						"TERME_CHEMIN_RE"
<concat_er>		= <concat> "." <term_er> ;					"TERME_CHEMIN_RE_DOT"
<concat_er>		= <concat_er> "-" <term> ;					"TERME_CHAMP_RE"
<concat_er>		= <concat_er> "-" <term_er> ;					"TERME_CHAMP_RE"
<concat_er>		= <concat> "-" <term_er> ;					"TERME_CHAMP_RE"

<term>			= <facteur> ;

<term_er>		= <facteur_er> "*" ;						"KLEENE"
<term_er>		= <facteur> "*" ;						"KLEENE"
<term_er>		= <facteur_er> ;

<facteur>		= "(" <concat> ")" ;
<facteur>		= <champ> ;	

<facteur_er>		= "(" <concat_er> ")" ;
<facteur_er>		= "(" <ou_LIST> ")" ;

<ou_LIST>		= <ou_LIST> "|" <chemin> ;
<ou_LIST>		= <chemin> "|" <chemin> ;					"OU_S"

<chemin> 		= <term> ;
<chemin> 		= <term_er> ;

<champ>			= <graphie> ;
<champ>			= %ENTIER ;

<graphie>		= <IDENT> ;
<graphie>		= %CHAINE ;

<IDENT>			= %IDENT ;
<IDENT>			= pred ;							"PRED"
* <IDENT>		= lex ;								"LEX"

