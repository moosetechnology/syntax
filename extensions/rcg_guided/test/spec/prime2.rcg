%
% L = {w | |w| is a prime et w \in T^*}
%
% Temps d'analyse n log n (theorique et pratique)
% La verif que a^19489 est premier prend 1s sur ma SS10
%

S(X)		--> Prime(X) .
S(X)		--> T(X, X) . % sert uniquement a definir l'ensemble T
% Peut etre qu'avoir le predicat predefini $False, qui retourne toujours faux
% (La clause qui le contient est inutile et donc jamais activee)
% serait + propre.
% S(X)		--> T(X) &False() .
% T(\a...\z)	--> .

% Clause bidon qui n'est jamais vraie...
% Le 1er arg de T contient la liste de tous les symboles terminaux
T(\a, )		--> .

% Prime est defini sans aucune reference explicite aux elements de T
Prime(X)	--> !NotPrime(X) .

%
% 0 n'est pas un nombre premier
% Un nombre n strictement positif n'est pas un nombre premier ssi il existe
% deux entiers k et x tels que n = k*x et 1 < x, k < n
%
% Plus precisement ces nombres k et x doivent etre compris entre 2 et n^{1/2}
% 2 <= x, k <= n^{1/2}
%


% 0 n'est pas un nombre premier
NotPrime()		--> .
% 1, 2 et 3 sont premiers
% Si n est tel qu'il existe des nombres x1, x2, x3 et k t.q. x1+x2+x3=n, x1=x2, x1>=2, x1^2 <= n
% et x3=k*x1 alors n n'est pas premier
%
% Complexite : il y a n/2 decoupages de la chaine source (&StrEqLen(X1, X2))
% Pour un decoupage donne (2 <= x1 <=n/2), deux cas peuvent se produire soit x1 > n^{1/2}
% soit x1 <= n^{1/2}.  Si x1 > n^{1/2}, XYleqZ retourne faux et Mul n'est pas execute (on
% suppose que les predicats s'executent de gauche a droite). Si x1 <= n^{1/2}, XYleqZ retourne
% vrai et Mul s'execute.  On a donc
%   - pour XYleqZ 	\Sigma_{x1=n^{1/2}}^{n/2} n/x1		% valeur FALSE
%                	+\Sigma_{x1=2}^{n^{1/2}} x1		% valeur TRUE
%                	= O(nlogn)+O(n)
%   - pour Mul		\Sigma_{x1=2}^{n^{1/2}} n/x1
%			= O(nlogn)
%
% Soit au total		O(nlogn)
%

NotPrime(X1 X2 Y Z)	--> &StrLen(2, X1)
			    &StrEqLen(X1 X2, Y)
			    XyleqZ(Y, Y, X1 X2 Y Z)
			    Mul(Y, Z) .

% XYleqZ(X, Y, Z) <=> |X|*|Y| <= |Z|
% Soit |x| = max (|X|, |Y|) et |y| = min (|X|, |Y|)
% Complexite en |y| quand ca marche
% Complexite en |Z|/|x| quand ca marche pas
% Le veritable appel se fait avec |X| >= |Y|
% XYleqZ(X1 X2, Y, Z)		--> &StrEqLen(X1, Y) XyleqZ(X1 X2, Y, Z) .
% XYleqZ(X, Y1 Y2 Y3, Z)	--> &StrEqLen(X, Y1) &StrLen(1, Y3) XyleqZ(Y1 Y2 Y3, X, Z) .
% vrai si |Y| = 0
XyleqZ(X, , Z)			--> .
% vrai si |X| >= |Y| >= 1 et |Z| >= |X| et si |X|*(|Y|-1) <= |Z|-|X|
XyleqZ(X, Y1 Y2, Z1 Z2)	--> &StrLen(1, Y2) &StrEqLen(X, Z2) XyleqZ(X, Y1, Z1) .

% Mul (X,Y) regarde si |Y| est un multiple de |X|. On suppose que X est non vide.
% Complexite |Y|/|X|
% 0 est un multiple de |X|
Mul(X, )		--> .
% Un nombre |Y1Y2| est un multiple de |X|>0 si |X|==|Y2| et si |Y1| est un multiple de |X|
Mul(X, Y1 Y2)		--> &StrEqLen (X, Y2) Mul(X, Y1) .
