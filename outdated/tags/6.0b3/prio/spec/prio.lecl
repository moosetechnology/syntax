Classes

	ANY		= #000..#377 ;
	PRINT		= ANY - #000..#040 - #177..#241 ;
	T_HEADER	= PRINT - "<#;\"~&@" ;
	NT_BODY		= PRINT + SP - ">" ;
	OCTAL		= "0".."7" ;


Abbreviations

	NUMBER_NORMAL_FORM
			= @Mark {"0"&True}+ @Erase {DIGIT}+ | "0"&True | {DIGIT}+ ;


Tokens

	Comments	= -{SP | HT | VT | FF | EOL |
			    "~" {^"~\n" | {EOL}+ ^"<~\n"} {EOL} "~" |
			    "*"&Is_First_Col {^EOL} EOL}+ ;
			   Priority Shift>Reduce ;
			   Context All But Comments ;

	%LHS_NON_TERMINAL
			= "<"&Is_First_Col {NT_BODY} ">"  |
			  "@"&Is_First_Col NUMBER_NORMAL_FORM ;

	%NON_TERMINAL	= "<" {NT_BODY} ">" |
			  "@" NUMBER_NORMAL_FORM ;
			Priority Shift>Reduce ;

	%TERMINAL	= (-"#" PRINT | T_HEADER) {PRINT} |
			   -QUOTE {^"\"\n\\" |
				   -"\\" {-EOL -"\\"}
					 (-EOL ^"\"\n\\<" |
					  -"n" @Put (EOL) |
					  -"b" @Put (BS) |
					  -"t" @Put (HT) |
					  -"v" @Put (VT) |
					  -"f" @Put (FF) |
					  -"r" @Put (CR) |
					  @Mark OCTAL&True [OCTAL&True [OCTAL&True]] @1 |
					  ^"bnfrtv\n"
					 )
				  }+ {-"\\" -EOL} -QUOTE ;
			   Priority Shift>Reduce ;
			   Context Comments, Eof ;

	%PREDICATE	= "&" NUMBER_NORMAL_FORM ;
			   Context Comments ;

-- afin de detecter une erreur si un mot cle en colonne 1 est mal ecrit
	KEY_WORD	= "%"&Is_First_Col {LOWER}+ ;
			   Context Comments ;

	"="		= "=" ;
			   Priority Reduce>Reduce, Shift>Reduce ;
			   Context Comments, Eof ;


--  @1  : Convert octal code \nnn to character
