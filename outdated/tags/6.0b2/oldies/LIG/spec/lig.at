*
*	This grammar is used for the specification of LIGs
*
<LIG>		= <RULE_LIST>						; "LIG"
*
<RULE_LIST>	= <RULE_LIST>
		  <RULE> 						; 
<RULE_LIST>	= <RULE> 						; "RULE_S"
*
<RULE>		= <LHS_OBJECT>  <=>  <VOCABULARY_LIST>  <;>		; "RULE"
*
<VOCABULARY_LIST>
		= <VOCABULARY_LIST>  <VOCABULARY> 			;
<VOCABULARY_LIST>
		= 				 			; "VOCABULARY_S"
*
<VOCABULARY>	= %NON_TERMINAL  <OBJECT>  <PREDICATE>			; "RHS_OBJECT"
<VOCABULARY>	= <TERMINAL>  <PREDICATE>				; "X_TERMINAL"
<VOCABULARY>	= %ACTION						; "ACTION"
*
<LHS_OBJECT>	= %LHS_NON_TERMINAL  <OBJECT>  <ACTION>			; "LHS_OBJECT"
*
<TERMINAL>	= %TERMINAL						; "TERMINAL"
<TERMINAL>	= %GENERIC_TERMINAL					; "GENERIC_TERMINAL"
*
* For attached comments
<=>		= =							;
*
<;>		= ";"							;
*
<T_LIST>	= 							; "TERMINAL_S"
<T_LIST>	= <T_LIST>  %TERMINAL					;
*
<PREDICATE>	=							;
<PREDICATE>	= %PREDICATE						; "PREDICATE"
*
<ACTION>	=							;
<ACTION>	= %ACTION						; "ACTION"
*
<OBJECT>	= 							;
<OBJECT>	= "("  ".."  <T_LIST>  ")"				; "P_OBJECT"
<OBJECT>	= "("  <T_LIST>  ")"					; "S_OBJECT"

