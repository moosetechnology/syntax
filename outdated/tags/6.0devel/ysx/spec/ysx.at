<spec>	= <def_LIST> %MARK <rule_LIST> <tail>	;		"YACC_SPEC"

<tail>	= %MARK	;
<tail>	=	;

<def_LIST>	=			;			"DEFS"
<def_LIST>	= <def_LIST> <def>	;

<def>	= "%start" %ID			;			"START"
<def>	= %UNION			;
<def>	= %LCURL			;
<def>	= "%left" <n_LIST>		;			"LEFT_PRIO"
<def>	= "%right" <n_LIST>		;			"RIGHT_PRIO"
<def>	= "%nonassoc" <n_LIST>		;			"NONASSOC_PRIO"
<def>	= "%token" <n_LIST>		;			"TOKENS"
<def>	= "%token" <tag> <n_LIST>	;			"TYPED_TOKENS"
<def>	= "%type" <tag> <id_LIST>	;			"TYPE"

<tag>	= "<" %ID ">"	;

<n_LIST>	= <nmno>		;			"TOKEN_LIST"
<n_LIST>	= <n_LIST> <nmno>	;
<n_LIST>	= <n_LIST> "," <nmno>	;

<nmno>	= %ID		;
<nmno>	= %LIT		;
<nmno>	= %ID "NUM"	;					"ID_NUM"
<nmno>	= %LIT "NUM"	;					"LIT_NUM"

<id_LIST>	= %ID			;			"ID_LIST"
<id_LIST>	= <id_LIST> %ID		;
<id_LIST>	= <id_LIST> "," %ID	;

<rule_LIST>	= <leading_rule>	;			"RULES"
<rule_LIST>	= <rule_LIST> <rule>	;

<leading_rule>	= %C_ID ":" <rhs_LIST> <prec>	;		"LEADING_RULE"

<rule>	= <leading_rule>		;
<rule>	= "|" <rhs_LIST> <prec>		;			"FOLLOWING_RULE"

<rhs_LIST>	=			;			"RIGHT_HAND_SIDE"
<rhs_LIST>	= <rhs_LIST> %ID	;
<rhs_LIST>	= <rhs_LIST> %LIT	;
<rhs_LIST>	= <rhs_LIST> %ACTION	;

<prec>	=			;
<prec>	= "%prec" %ID		;
<prec>	= "%prec" %LIT		;
<prec>	= "%prec" %ID %ACTION	;				"POST_ACTION"
<prec>	= "%prec" %LIT %ACTION	;				"POST_ACTION"
<prec>	= <prec> ";"		;
