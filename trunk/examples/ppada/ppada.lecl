-- /***************************************************************************
-- *                                S Y N T A X
-- *---------------------------------------------------------------------------
-- *   Copyright (C) 1972-2023 INRIA (Institut National de Recherche en
-- *   Informatique et Automatique)
-- *--------------------------------------------------------------------------
-- *   URL: http://sourcesup.renater.fr/projects/syntax
-- *--------------------------------------------------------------------------
-- *   The source code of SYNTAX is distributed with two different licenses,
-- *   depending on the files:
-- *   - The recursive content of src/ and incl/ and the non-recursive content
-- *     of SYNTAX's root directory are distributed under the CeCILL-C license
-- *   - The recursive content of all other repertories is distributed under
-- *     the CeCILL license
-- *   All code produced by SYNTAX must be considered as being under the
-- *   CeCILL-C license. Information about the CeCILL and CeCILL-C licenses
-- *   can be found at, e.g., http://www.cecill.info
-- **************************************************************************/

Classes

	PERCENT		= "%" ;
	EOL		= NL + VT + FF + CR ;
	EXTENDED_DIGIT	= DIGIT + A + B + C + D + E + F ;
	SEPARATOR	= EOL + HT ;
	SEP_Q		= SEPARATOR + QUOTE ;
	SEP_PC		= SEP_Q + PERCENT ;
	SPACE		= SP + HT ;


Abbreviations

	INTEGER		= DIGIT {["_"] DIGIT} ;
	X_INT		= EXTENDED_DIGIT {["_"] EXTENDED_DIGIT} ;
	BASE_1		= ":" @Set (1) | "#" @Reset (1) ;
	BASE_2		= ":"&Is_Set (1) | "#"&Is_Reset (1) ;
	EXPONENT	= E ["-" | "+"] INTEGER ;
	QUOTE_1		= QUOTE @Set (2) ;
	QUOTE_2		= QUOTE&Is_Set (2) ;
	PERCENT_1	= PERCENT @Reset (2) ;
	PERCENT_2	= PERCENT&Is_Reset (2) ;
	COMMENT		= "-" "-" {^EOL} EOL | EOL ;


Tokens

	Comments	= -{SPACE}+ | {COMMENT {SPACE}}+ @4 ;
			   -- `--... EOL ---' is a comment, not a comment followed by `-' followed by a comment:
			   Priority Shift>Reduce ;
			   Unbounded Context All But Comments ;
	"-"		= "-" ;
			   -- `--....' is a comment and not the minus operator followed by a comment:
			   Priority Shift>Reduce ;
	%IDENTIFIER	=
	     Union
		LETTERS : {LOWER}+ | {LOWER} UPPER {LETTER} @1 ;
		OTHERS	: {LETTER}+ (DIGIT | "_" (LETTER | DIGIT)) {["_"] (LETTER | DIGIT)} ;
	     End ;
			   Priority Shift>Reduce ;
	%NUM_LITERAL	=
	     Union
		INT	: INTEGER [EXPONENT] ;
		X_INT	: INTEGER BASE_1 X_INT BASE_2 [EXPONENT] ;
		REAL	: INTEGER "." INTEGER [EXPONENT] ;
		X_REAL	: INTEGER BASE_1 X_INT "." X_INT BASE_2 [EXPONENT] ;
	     End ;
			   Unbounded Context All But %IDENTIFIER.LETTERS ;
	%CHAR_STRING	=
	     Union
		NORMAL	: QUOTE_1 {^SEP_Q} QUOTE_2 [QUOTE {^SEP_Q | QUOTE QUOTE} QUOTE] ;
		PERCENT : PERCENT_1 {^SEP_PC} PERCENT_2 [PERCENT {^SEP_PC | PERCENT PERCENT} PERCENT] ;
	     End ;
	%OP_STRING	= (QUOTE_1 | PERCENT_1) (A N D | O R | X O R | M O D | R E M | A B S | N O T | "=" | "/" "=" | "<"
	     | "<" "=" | ">" | ">" "=" | "+" | "-" | "&" | "*" | "/" | "*" "*") (QUOTE_2 | PERCENT_2) ;
			   Priority Shift>Reduce, Reduce>Reduce ;
	%CHAR_LITERAL	= "'" ^SEPARATOR "'" ;
	"|"		= "|!" ;
	"'"		= -"'"&1 ;



-- &1 : true if previous token is %IDENTIFIER or ")"
-- @1 : recognize keywords when not lowercased
-- @4 : Comments: rtrim ( SPACE, EOL ).

