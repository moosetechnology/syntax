*
*	This grammar is used for the specification of BNF + PARADIS
*
<BNF>		= <RULE_LIST>				;
*
<RULE_LIST>	= <RULE_LIST>
		  <RULE> 				; 
<RULE_LIST>	= <RULE> 				; "RULE_S"
*
<RULE>		= <VOCABULARY_LIST>  ";"		;
*
<VOCABULARY_LIST>
		= <VOCABULARY_LIST>  <VOCABULARY> 	;
<VOCABULARY_LIST>
		= LHS_NON_TERMINAL  %LHS	 	; "VOCABULARY_S"
*
<VOCABULARY>	= %NON_TERMINAL 			; "NON_TERMINAL"
<VOCABULARY>	= %ACTION				; "ACTION"
<VOCABULARY>	= %TERMINAL				; "TERMINAL"
<VOCABULARY>	= %GENERIC_TERMINAL			; "GENERIC_TERMINAL"
<VOCABULARY>	= %PREDICATE				;
<VOCABULARY>	= %BLANKS				;
<VOCABULARY>	= "~" <PP_SPEC> "~"			; "PP_SPEC"
*
<PP_SPEC>	=  					;
<PP_SPEC>	= INH 					; "INHIBITION"
<PP_SPEC>	= MARGIN <DELTA> 			; "MARGIN"
<PP_SPEC>	= SPACE  <FORWARD_MOVE_SPEC> 		; "SPACE"
<PP_SPEC>	= SPACE  <BACKWARD_MOVE_SPEC>		; "SPACE"
<PP_SPEC>	= COL    <FORWARD_MOVE_SPEC> 		; "COLUMN"
<PP_SPEC>	= SKIP   <FORWARD_MOVE_SPEC>		; "NEW_LINE"
<PP_SPEC>	= PAGE   <FORWARD_MOVE_SPEC> 		; "FORM_FEED"
<PP_SPEC>	= TAB    <FORWARD_MOVE_SPEC> 		; "TABULATION"
*
<FORWARD_MOVE_SPEC>
		= 					;
<FORWARD_MOVE_SPEC>
		= ( %NUMBER ) 				; "FORWARD"
*
<BACKWARD_MOVE_SPEC>
		= ( - %NUMBER ) 			; "BACKWARD"
*
<DELTA>		= + %NUMBER				; "FORWARD"
<DELTA>		= - %NUMBER				; "BACKWARD"
<DELTA>		= 					;
*
